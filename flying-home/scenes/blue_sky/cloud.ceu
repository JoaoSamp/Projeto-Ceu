var IVector sprite_size_cloud			= val IVector (32, 32);
var& SDL_Open_Image img_idle_cloud 		= spawn SDL_Open_Image(&renderer, "assets/img/tiles/smallCloud-idle.png");
var& SDL_Open_Image img_destroy_cloud 	= spawn SDL_Open_Image(&renderer, "assets/img/tiles/smallCloud-destroy.png");
var IImage_Quad idle_quad_cloud			= val IImage_Quad( &img_idle_cloud, sprite_size_cloud, _ );

data ITile.ICloud;

code/call Create_Cloud ( var IVector start_pos, var IVector speed) -> ITile.ICloud
do
	var IVector size 			= val IVector (26, 26);
	var IVector sprite_size		= val IVector (32, 32);
	var IVector sprite_offset	= val IVector (16, 16);
    var IMoving_Obj imov_obj     = call Create_Moving_Obj( start_pos, size);

	var IImage_Quad iq_destroy		= val IImage_Quad( &outer.img_destroy_cloud, outer.sprite_size_cloud, _ );
    var IAnimation ianim_destroy    = val IAnimation( iq_destroy, sprite_offset, 12 );

    imov_obj.speed 				= speed;
    var ITile.ICloud tile 		= val ITile.ICloud (imov_obj, sprite_offset, &outer.idle_quad_cloud, ianim_destroy, _, _, _, _);
    escape tile;
end

code/await Cloud(var& ITile.ICloud itile) -> none
do
	await Common_Tile (&itile);
end

code/await/dynamic Tile (var&/dynamic ITile.ICloud t) -> (var ITile.ICloud itile = t) -> none
do
	await Cloud (&itile);
end

code/await/dynamic Hit_Tile (var&/dynamic IActor.IPlayer iactor, var&/dynamic ITile.ICloud itile) -> none
do
	emit iactor.hit;
	emit itile.hit;
	await All_Speed_Effect (-20, 3000, &iactor);
	emit itile.destroy;
end