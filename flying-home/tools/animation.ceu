code/await Animation (var Image_Quad i_quad, var& Vector position,
                        var& Vector scale, var& Vector offset, var int fps) -> none
do
    par/or do
        every (1000/fps)ms do
            i_quad.frame.x = i_quad.frame.x + i_quad.size.x;
            if i_quad.frame.x >= i_quad.img.tex.width then
                i_quad.frame.x = 0;
                i_quad.frame.y = i_quad.frame.y + i_quad.size.y;
                if i_quad.frame.y >= i_quad.img.tex.height then
                    i_quad.frame.y = 0;
                    break;
                end
            end
        end
    with
        every SDL_REDRAW do
            call Draw(&i_quad, position.x, position.y, scale.x, scale.y, offset.x, offset.y);
        end
    end
end

code/await Loop_Animation (var& Image_Quad i_quad, var& Vector position,
                            var& Vector scale, var& Vector offset, var int fps) ->
                                (var& Image_Quad anim_quad = &i_quad, event bool pause_animation, event bool pause_draw) -> NEVER
do
    par/or do
        pause/if pause_animation do      
            every (1000/fps)ms do
                i_quad.frame.x = i_quad.frame.x + i_quad.size.x;
                if i_quad.frame.x >= i_quad.img.tex.width then
                    i_quad.frame.x = 0;
                    i_quad.frame.y = i_quad.frame.y + i_quad.size.y;
                    if i_quad.frame.y >= i_quad.img.tex.height then
                        i_quad.frame.y = 0;
                    end
                end
            end
        end
    with
        pause/if pause_draw do
            every SDL_REDRAW do
                call Draw(&i_quad, position.x, position.y, scale.x, scale.y, offset.x, offset.y);
            end
        end
    end
end