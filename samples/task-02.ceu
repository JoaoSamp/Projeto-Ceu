// Move image

#include "c.ceu"
#include "sdl/sdl.ceu"

var SDL_Color bg = val SDL_Color(0x00,0x00,0x00,0xFF);
var SDL_Rect rect = val SDL_Rect(100,100,20,50);
var SDL_Rect img_r = val SDL_Rect(100,100, 32,32);
var int velX = 0;
var int velY = 0;
var int vel = 10;

var&? SDL_Init sdl = spawn SDL_Init("Task 02 - WASD-Move  UP/DOWN-Change Speed", 800,480, bg);
watching sdl do
	par do		
		var _SDL_KeyboardEvent&& key;
            every key in SDL_KEYDOWN do
                if key:keysym.sym == _SDLK_w  then
                        velY = -1;
                else/if key:keysym.sym == _SDLK_s then
                        velY = 1;
                else/if key:keysym.sym == _SDLK_d then
			velX = 1;
                else/if key:keysym.sym == _SDLK_a then
                        velX = -1;
                else/if key:keysym.sym == _SDLK_UP then
                        vel = vel-1;
                else/if key:keysym.sym == _SDLK_DOWN then
                        vel = vel+1;
                end
            end
	with
		var _SDL_KeyboardEvent&& key;
		every key in SDL_KEYUP do
			if key:keysym.sym == _SDLK_w and velY < 0 then
                velY = 0;
            else/if key:keysym.sym == _SDLK_s and velY > 0 then
                velY = 0;
            else/if key:keysym.sym == _SDLK_d and velX > 0 then
                velX = 0;
            else/if key:keysym.sym == _SDLK_a and velX < 0 then
                velX = 0;			
			end
		end
	with
		every (vel)ms do
			img_r.x = img_r.x + velX;
			img_r.y = img_r.y + velY;
		end
	with 
        var& _SDL_Texture img_tex =
                &_IMG_LoadTexture(&&sdl.ren, "idle-down.png")
                finalize (img_tex) with
                        _SDL_DestroyTexture(&&img_tex);
                end;

		every SDL_REDRAW do
			_SDL_RenderCopy(&&sdl.ren, &&img_tex, null, &&img_r as _SDL_Rect&&);
		end
    end
end


escape 0;
